$version 9.30

// Bitmap resource containing the border (frame) of all panels used within the demo.
$rect <280,180,480,220>
$output false
resource Resources::Bitmap PanelBorder
{
  attr bitmapfile FileName;
  attr alphafile AlphaName = .\Res\Border.png;
  attr framesize FrameSize;
  attr framedelay FrameDelay;
}

// Enumeration definition of supported waveforms.
$rect <280,60,480,100>
$output false
enum WaveformType
{
  $rect <10,10,150,50>
  item Ground;

  $rect <160,10,300,50>
  item Sinus;

  $rect <310,10,450,50>
  item Square;

  $rect <10,60,150,100>
  item Ramp;

  $rect <160,60,300,100>
  item Pulse;

  $rect <310,60,450,100>
  item Noise;
}

// Bitmap resource containing the icons for the waveform selction buttons.
$rect <280,260,480,300>
$output false
resource Resources::Bitmap WaveformIcons
{
  attr bitmapfile FileName;
  attr alphafile AlphaName = .\Res\WaveformIcons.png;
  attr framesize FrameSize = <24,24>;
  attr framedelay FrameDelay;
}

// Bitmap resource to create a soft fade-out within scrollable channel list.
$rect <280,220,480,260>
$output false
resource Resources::Bitmap Fader
{
  attr bitmapfile FileName;
  attr alphafile AlphaName = .\Res\Fader.png;
  attr framesize FrameSize;
  attr framedelay FrameDelay;
}

// Panels
note group Note2
{
  attr Bounds = <10,130,250,320>;
}

// Type definition
note group Note3
{
  attr Bounds = <260,10,500,120>;
}

// Waveform class
note group Note4
{
  attr Bounds = <10,10,250,120>;
}

// Bitmaps
note group Note5
{
  attr Bounds = <260,130,500,320>;
}

// The sample application 'WaveformGenerator' is focused on the calculation and presentation \
// of dynamic data as graph diagramm by using the Charts::Graph class.
// 
// Within this example, two data sources ("Channel1" and "Channel2") can be configured \
// to provide a sine, square, ramp or noise signal with a certain amplitude and frequency. \
// These two signals are then added and used to calculate the list of coordinates \
// for the graph. Each time the signal has changed, a complete set of coordinates \
// is calulated and the graph is updated.
// 
// This waveform generator demo and the different assets are designed for a screen \
// size of 800x480 pixels.
note legend Note
{
  attr Bounds = <10,330,500,610>;
}

// The 'ChannelPanel' class implements a complete UI components for making all user \
// settings of a waveform generator channel.
$rect <30,180,230,220>
$output false
class ChannelPanel : Core::Group
{
  $rect <0,270,200,310>
  inherited property Bounds = <0,0,200,230>;

  // This is a bitmap frame view.
  $rect <20,20,160,60>
  object Views::Frame Frame
  {
    preset Bounds = <5,20,195,225>;
    preset Color = Waveform::ThemeColor;
    preset Bitmap = Waveform::PanelBorder;
  }

  $rect <20,20,160,60>
  object Views::Text CaptionText
  {
    preset Bounds = <5,0,170,30>;
    preset Alignment = Views::TextAlignment[AlignHorzLeft, AlignVertCenter];
    preset String = "Text";
    preset Font = Resources::FontMedium;
    preset Color = Waveform::ThemeColor;
  }

  $rect <20,20,160,60>
  object WidgetSet::HorizontalSlider SliderFrequency
  {
    preset Bounds = <17,20,182,70>;
    preset OnChange = OnSlider;
    preset Outlet = ^Frequency;
    preset MaxValue = 200;
    preset MinValue = 10;
    preset Appearance = WidgetSet::HorizontalSlider_Medium;
  }

  $rect <20,20,160,60>
  object WidgetSet::HorizontalSlider SliderAmplitude
  {
    preset Bounds = <17,80,182,130>;
    preset OnChange = OnSlider;
    preset Outlet = ^Amplitude;
    preset MaxValue = 50;
    preset Appearance = WidgetSet::HorizontalSlider_Medium;
  }

  // This is an ordinary text view.
  $rect <20,20,160,60>
  object Views::Text Text1
  {
    preset Bounds = <17,63,133,83>;
    preset Alignment = Views::TextAlignment[AlignHorzLeft, AlignVertCenter];
    preset String = "Frequency [Hz]";
    preset Font = Resources::FontSmall;
    preset Color = Waveform::ThemeColor;
  }

  // This is an ordinary text view.
  $rect <20,20,160,60>
  object Views::Text Text2
  {
    preset Bounds = <17,123,143,143>;
    preset Alignment = Views::TextAlignment[AlignHorzLeft, AlignVertCenter];
    preset String = "Amplitude [mV]";
    preset Font = Resources::FontSmall;
    preset Color = Waveform::ThemeColor;
  }

  // The property 'Frequency' stores the current frequency of the simulated waveform \
  // generator channel.
  $rect <210,50,410,90>
  property int32 Frequency = 100;

  // The property 'Amplitude' stores the current amplitude of the simulated waveform \
  // generator channel.
  $rect <210,90,410,130>
  property int32 Amplitude = 10;

  // The property 'Waveform' stores the currently selected waveform of the simulated \
  // waveform generator channel.
  $rect <210,270,410,310>
  property Waveform::WaveformType Waveform = Waveform::WaveformType.Sinus;

  // Slot method, called when the user changed a slider value.
  $rect <410,70,610,110>
  slot OnSlider
  {
    postsignal OnChanged;

  }

  // Slot method, called when the user pressed the corresponding waveform selection \
  // button.
  $rect <210,180,410,220>
  slot OnSinus
  {
    /* select new waveform */
    Waveform = Waveform::WaveformType.Sinus;

    postsignal OnChanged;

  }

  // The slot method 'OnChanged' is called each time the user has changed one of \
  // the current waveform generator channel settings.
  $rect <410,270,610,310>
  property slot OnChanged = null;

  $rect <20,20,160,60>
  object WidgetSet::RadioButton ButtonSinus
  {
    preset Bounds = <12,148,52,228>;
    preset Selected = true;
    preset OnSelect = OnSinus;
    preset IconOnFrameActive = 0;
    preset IconOnFrameFocused = 0;
    preset IconOnFrameDisabled = 0;
    preset IconOnFrameDefault = 0;
    preset IconOffFrameActive = 0;
    preset IconOffFrameFocused = 0;
    preset IconOffFrameDisabled = 0;
    preset IconOffFrameDefault = 0;
    preset Icon = Waveform::WaveformIcons;
    preset Appearance = CheckBox;
  }

  $rect <20,20,160,60>
  object WidgetSet::RadioButton ButtonSquare
  {
    preset Bounds = <57,148,97,228>;
    preset OnSelect = OnSquare;
    preset IconOnFrameActive = 1;
    preset IconOnFrameFocused = 1;
    preset IconOnFrameDisabled = 1;
    preset IconOnFrameDefault = 1;
    preset IconOffFrameActive = 1;
    preset IconOffFrameFocused = 1;
    preset IconOffFrameDisabled = 1;
    preset IconOffFrameDefault = 1;
    preset Icon = Waveform::WaveformIcons;
    preset Appearance = CheckBox;
  }

  $rect <20,20,160,60>
  object WidgetSet::RadioButton ButtonRamp
  {
    preset Bounds = <102,148,142,228>;
    preset OnSelect = OnRamp;
    preset IconOnFrameActive = 2;
    preset IconOnFrameFocused = 2;
    preset IconOnFrameDisabled = 2;
    preset IconOnFrameDefault = 2;
    preset IconOffFrameActive = 2;
    preset IconOffFrameFocused = 2;
    preset IconOffFrameDisabled = 2;
    preset IconOffFrameDefault = 2;
    preset Icon = Waveform::WaveformIcons;
    preset Appearance = CheckBox;
  }

  $rect <20,20,160,60>
  object WidgetSet::RadioButton ButtonNoise
  {
    preset Bounds = <147,148,187,228>;
    preset OnSelect = OnNoise;
    preset IconOnFrameActive = 3;
    preset IconOnFrameFocused = 3;
    preset IconOnFrameDisabled = 3;
    preset IconOnFrameDefault = 3;
    preset IconOffFrameActive = 3;
    preset IconOffFrameFocused = 3;
    preset IconOffFrameDisabled = 3;
    preset IconOffFrameDefault = 3;
    preset Icon = Waveform::WaveformIcons;
    preset Appearance = CheckBox;
  }

  $rect <410,0,610,40>
  onset Caption
  {
    /* store the given value */
    pure Caption = value;

    /* and assign it to the text item */
    CaptionText.String = value;
  }

  // The property 'Caption' stores the text to display within the item. 
  $rect <210,0,410,40>
  property string Caption = "Channel";

  // Slot method, called when the user pressed the corresponding waveform selection \
  // button.
  $rect <210,220,410,260>
  slot OnSquare
  {
    /* select new waveform */
    Waveform = Waveform::WaveformType.Square;

    postsignal OnChanged;

  }

  // Slot method, called when the user pressed the corresponding waveform selection \
  // button.
  $rect <410,180,610,220>
  slot OnRamp
  {
    /* select new waveform */
    Waveform = Waveform::WaveformType.Ramp;

    postsignal OnChanged;

  }

  // Slot method, called when the user pressed the corresponding waveform selection \
  // button.
  $rect <410,220,610,260>
  slot OnNoise
  {
    /* select new waveform */
    Waveform = Waveform::WaveformType.Noise;

    postsignal OnChanged;

  }

  $rect <210,310,410,350>
  onset Waveform
  {
    pure Waveform = value;

    /* set the buttons according given value */
    ButtonSinus.Selected  = value == Waveform::WaveformType.Sinus; 
    ButtonSquare.Selected = value == Waveform::WaveformType.Square; 
    ButtonRamp.Selected   = value == Waveform::WaveformType.Ramp; 
    ButtonNoise.Selected  = value == Waveform::WaveformType.Noise; 
  }

  // The ChannelPannel class contains all widgets to choose the current signal source \
  // and to configure the waveform paramters.
  // Each time the user has changed the waveform settings, a signal is sent to the \
  // slot method 'OnChanged'.
  note legend Note
  {
    attr Bounds = <0,360,600,460>;
  }

  // This object provides a customization for the 'toggle' widget (WidgetSet::ToggleButton) \
  // to show it as 'checkbox' with an icon below the checkbox.
  $rect <210,140,410,180>
  object WidgetSet::RadioButtonConfig CheckBox
  {
    preset LabelOnColorActive = #6C6E70FF;
    preset LabelOnColorFocused = #6C6E70FF;
    preset LabelOnColorDisabled = #A8ABABFF;
    preset LabelOnColorDefault = #6C6E70FF;
    preset LabelOffColorActive = #6C6E70FF;
    preset LabelOffColorFocused = #6C6E70FF;
    preset LabelOffColorDisabled = #A8ABABFF;
    preset LabelOffColorDefault = #6C6E70FF;
    preset LabelMarginLeft = 34;
    preset IconOnTintActive = Waveform::ThemeColor;
    preset IconOnTintFocused = Waveform::ThemeColor;
    preset IconOnTintDisabled = Waveform::ThemeColor;
    preset IconOnTintDefault = Waveform::ThemeColor;
    preset IconOffTintActive = Waveform::ThemeColor;
    preset IconOffTintFocused = Waveform::ThemeColor;
    preset IconOffTintDisabled = Waveform::ThemeColor;
    preset IconOffTintDefault = Waveform::ThemeColor;
    preset IconMarginTop = 44;
    preset LabelAlignment = Views::TextAlignment[AlignHorzLeft, AlignVertCenter];
    preset LabelOnFont = Resources::FontSmall;
    preset LabelOffFont = Resources::FontSmall;
    preset IconAlignment = Views::ImageAlignment[AlignHorzCenter, AlignVertTop];
    preset FaceLayout = Core::Layout[AlignToTop];
    preset FaceOnFrameActive = 7;
    preset FaceOnFrameFocused = 5;
    preset FaceOnFrameDisabled = 1;
    preset FaceOnFrameDefault = 3;
    preset FaceOnActive = WidgetSet::CheckBoxMedium;
    preset FaceOnFocused = WidgetSet::CheckBoxMedium;
    preset FaceOnDisabled = WidgetSet::CheckBoxMedium;
    preset FaceOnDefault = WidgetSet::CheckBoxMedium;
    preset FaceOffFrameActive = 6;
    preset FaceOffFrameFocused = 4;
    preset FaceOffFrameDisabled = 0;
    preset FaceOffFrameDefault = 2;
    preset FaceOffActive = WidgetSet::CheckBoxMedium;
    preset FaceOffFocused = WidgetSet::CheckBoxMedium;
    preset FaceOffDisabled = WidgetSet::CheckBoxMedium;
    preset FaceOffDefault = WidgetSet::CheckBoxMedium;
    preset WidgetMinSize = <26,26>;
  }
}

// This is the root component of the entire GUI Waveform.
$rect <30,60,230,100>
$output false
class Waveform : Core::Group
{
  $rect <10,500,210,540>
  inherited property Bounds = <0,0,800,480>;

  $rect <10,540,210,580>
  inherited method Init()
  {
    /* take the initial settings */
    signal SettingsChanged;
  }

  $rect <410,600,610,640>
  inherited property Focus = null;

  // This is a filled rectangle view.
  $rect <20,20,160,60>
  object Views::Rectangle Rectangle
  {
    preset Bounds = <0,0,800,480>;
    preset Color = #000000FF;
  }

  // This is a bitmap frame view.
  $rect <20,20,160,60>
  object Views::Frame Frame
  {
    preset Bounds = <5,25,590,470>;
    preset Color = Waveform::ThemeColor;
    preset Bitmap = Waveform::PanelBorder;
  }

  $rect <20,20,160,60>
  object Waveform::ChannelPanel ChannelPanel1
  {
    preset Bounds = <600,8,800,238>;
    preset Frequency = 100;
    preset Amplitude = 28;
    preset OnChanged = SettingsChanged;
    preset Caption = "Channel 1";
  }

  $rect <20,20,160,60>
  object Waveform::ChannelPanel ChannelPanel2
  {
    preset Bounds = <600,246,800,476>;
    preset Frequency = 71;
    preset Amplitude = 30;
    preset Waveform = Waveform::WaveformType.Square;
    preset OnChanged = SettingsChanged;
    preset Caption = "Channel 2";
  }

  // The slot method 'SettingsChanged' is called when the user has changed the current \
  // waveform gernator channel settings.
  $rect <410,500,610,540>
  slot SettingsChanged
  {
    /* recalculate all coordinates */
    CalculatePath();

    /* update description of channel 1 */
    TextCh1.String = "Ch1:\nf = " + string( ChannelPanel1.Frequency ) + " Hz\na = " + string( ChannelPanel1.Amplitude ) + " mV\n";

    if ( ChannelPanel1.Waveform == Waveform::WaveformType.Sinus )
      TextCh1.String = TextCh1.String + "sine wave";
    else if ( ChannelPanel1.Waveform == Waveform::WaveformType.Square )
      TextCh1.String = TextCh1.String + "square wave";
    else if ( ChannelPanel1.Waveform == Waveform::WaveformType.Ramp )
      TextCh1.String = TextCh1.String + "ramp wave";
    else
      TextCh1.String = TextCh1.String + "white noise";

    /* update description of channel 2 */
    TextCh2.String = "Ch2:\nf = " + string( ChannelPanel2.Frequency ) + " Hz\na = " + string( ChannelPanel2.Amplitude ) + " mV\n";

    if ( ChannelPanel2.Waveform == Waveform::WaveformType.Sinus )
      TextCh2.String = TextCh2.String + "sine wave";
    else if ( ChannelPanel2.Waveform == Waveform::WaveformType.Square )
      TextCh2.String = TextCh2.String + "square wave";
    else if ( ChannelPanel2.Waveform == Waveform::WaveformType.Ramp )
      TextCh2.String = TextCh2.String + "ramp wave";
    else
      TextCh2.String = TextCh2.String + "white noise";

    /* if any noise waveform is selected, we need an continuous update... */
    RecalcTimer.Enabled = ( ChannelPanel1.Waveform == Waveform::WaveformType.Noise 
                         || ChannelPanel2.Waveform == Waveform::WaveformType.Noise );



  }

  // This is a timer object to trigger continuous recalculation of coordinates in \
  // case of a noise waveform.
  $rect <10,600,210,640>
  object Core::Timer RecalcTimer
  {
    preset OnTrigger = TimerSlot;
    preset Period = 250;
  }

  // Slot method to force the recalculation of coordinates in case of a noise waveform.
  $rect <210,600,410,640>
  slot TimerSlot
  {
    /* just make the mathematics... */
    CalculatePath();
  }

  // The main class of the demo contains a Views::StrokePath object to display the \
  // resulting waveform by adding the signals of channel 1 and 2. The method 'CalculatePath' \
  // is responsible to calculate the path of the simulated waveform.
  // The right part of the application contains an two panels: ChannelPanel1 and \
  // ChannelPanel2 to change the waveform parameters of each channel.
  note legend Note
  {
    attr Bounds = <10,650,610,780>;
  }

  // This is a slot method.
  $rect <620,540,820,580>
  slot AnimateFrequency
  {
    sender; /* the method is called from the sender object */
    notifyobservers ^ChannelPanel1.Frequency;
    postsignal SettingsChanged;


  }

  // This is an int32 change effect.
  $rect <620,500,820,540>
  object Effects::Int32Effect AutoDemoTimer
  {
    preset OnAnimate = AnimateFrequency;
    preset Symmetric = true;
    preset CycleDuration = 12000;
    preset InitialDelay = 0;
    preset Value2 = 140;
    preset Value1 = 60;
    preset Outlet = ^ChannelPanel1.Frequency;
  }

  // The method 'CalculatePath' contains all the mathematics to calculate the waveform \
  // path (= all graph coordinates) according the current waveform generator settings.
  $rect <210,500,410,540>
  method void CalculatePath()
  {
    var float f1 = (float)ChannelPanel1.Frequency;
    var float a1 = (float)ChannelPanel1.Amplitude / 20.0;

    var float f2 = (float)ChannelPanel2.Frequency;
    var float a2 = (float)ChannelPanel2.Amplitude / 20.0;

    var float r1 = 0.0;
    var float r2 = 0.0;

    var float x = 0.0;
    var float y = 0.0;
    var float yOffset = (float)StrokePath.Bounds.h / 2.0;
    var float xFactor = (float)StrokePath.Bounds.w / 4.0;
    var float yFactor = (float)StrokePath.Bounds.h / 8.0;

    /* clear the path */
    Path.InitSubPath( 0, 8 * 20 + 2 );

    /* calculate values for four divisions in horizontal direction */
    while ( x < 8.0 )
    {
      /* calculate result of channel 1 - according selected waveform */
      if ( ChannelPanel1.Waveform == Waveform::WaveformType.Sinus )
        r1 = math_sin( f1 * x * 360.0 / 100.0) * a1;
      else if ( ChannelPanel1.Waveform == Waveform::WaveformType.Square )
        r1 = ((float)((int32)( f1 * x * 2.0 / 100.0 ) & 0x01 ) * 2.0 - 1.0 ) * a1;
      else if ( ChannelPanel1.Waveform == Waveform::WaveformType.Ramp )
        r1 =  ((float)((int32)( f1 * x * 1.0 ) % 100 ) / 100.0 * (-2.0) + 1.0 ) * a1;
      else
        r1 = (float)math_rand( -100, 100 ) * a1 / 100.0;

      /* calculate result of channel 2 - according selected waveform */
      if ( ChannelPanel2.Waveform == Waveform::WaveformType.Sinus )
        r2 = math_sin( f2 * x * 360.0 / 100.0) * a2;
      else if ( ChannelPanel2.Waveform == Waveform::WaveformType.Square )
        r2 = ((float)((int32)( f2 * x * 2.0 / 100.0 ) & 0x01 ) * 2.0 - 1.0 ) * a2;
      else if ( ChannelPanel2.Waveform == Waveform::WaveformType.Ramp )
        r2 =  ((float)((int32)( f2 * x * 1.0 ) % 100 ) / 100.0 * (-2.0) + 1.0 ) * a2;
      else
        r2 = (float)math_rand( -100, 100 ) * a2 / 100.0;

      /* add the sum of both results */
      y = ( r1 + r2 ) * yFactor + yOffset;

      /* start the path */
      if ( x == 0.0 )
        Path.Begin( 0, x * xFactor, y );

      /* add a new line segment to the path */
      Path.AddLine( 0, x * xFactor, y );

      /* 20 points per division */
      x = x + 0.05;
    }

  }

  $rect <20,20,160,60>
  object Views::Line LineCenter
  {
    preset Point2 = <576,250>;
    preset Point1 = <15,250>;
    preset Color = #0C0E6EFF;
  }

  $rect <20,20,160,60>
  object Views::Line LineTop
  {
    preset Point2 = <576,93>;
    preset Point1 = <15,93>;
    preset Color = #0C0E6EFF;
  }

  $rect <20,20,160,60>
  object Views::Line LineBotom
  {
    preset Point2 = <576,407>;
    preset Point1 = <15,407>;
    preset Color = #0C0E6EFF;
  }

  $rect <210,540,410,580>
  object Graphics::Path Path;

  $rect <20,20,160,60>
  object Views::StrokePath StrokePath
  {
    preset Layout = Core::Layout[AlignToBottom, AlignToLeft, AlignToRight, AlignToTop, ResizeHorz, ResizeVert];
    preset Bounds = <15,40,576,460>;
    preset Width = 2.5;
    preset Path = Path;
  }

  // How to use vector graphics?
  // https://doc.embedded-wizard.de/stroked-path-view
  // https://doc.embedded-wizard.de/path-data
  note legend Note8
  {
    attr Bounds = <820,400,1200,480>;
  }

  // This is an ordinary text view.
  $rect <20,20,160,60>
  object Views::Text TextCh1
  {
    preset Bounds = <15,250,310,460>;
    preset Alignment = Views::TextAlignment[AlignHorzLeft, AlignVertBottom];
    preset String = "Ch1:";
    preset Font = Resources::FontLarge;
    preset Color = Waveform::ThemeColor;
  }

  // This is an ordinary text view.
  $rect <20,20,160,60>
  object Views::Text TextCh2
  {
    preset Bounds = <310,250,580,460>;
    preset Alignment = Views::TextAlignment[AlignHorzRight, AlignVertBottom];
    preset String = "Ch1:";
    preset Font = Resources::FontLarge;
    preset Color = Waveform::ThemeColor;
  }

  $rect <20,20,160,60>
  object Views::Text Caption
  {
    preset Bounds = <5,0,570,40>;
    preset Alignment = Views::TextAlignment[AlignHorzLeft, AlignVertCenter];
    preset String = "Waveform Generator Demo";
    preset Font = Resources::FontLarge;
    preset Color = Waveform::ThemeColor;
  }
}

$rect <30,260,230,300>
$output false
const color ThemeColor = #B2D339FF;
